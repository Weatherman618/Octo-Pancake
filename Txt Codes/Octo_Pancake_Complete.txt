#Octo-Pancake -Buys dips, Sells Rips
#Created by: Daniel Weatherly
#created: 3/22/19

#inputs
input price = close;
input displace = 0;
input length = 20;
input stop = 2;
input Num_Dev_Dn = -2.0;
input Num_Dev_up = 2.0;
input averageType = AverageType.SIMPLE;

#Labels
AddLabel( yes, "Octo-Pancake", Color.WHITE);
AddLabel( yes, "Bull-Bear", Color.WHITE);

#Declarations
def sDev = StDev(data = price[-displace], length = length);
def ATR = MovingAverage(averageType, TrueRange(high, close, low), length);

#STD Dev Bands
plot MidLine = MovingAverage(averageType, data = price[-displace], length = length);
plot LowerBand = MidLine + Num_Dev_Dn * sDev;
plot UpperBand = MidLine + Num_Dev_up * sDev;



#Long Orders
AddOrder(OrderType.BUY_TO_OPEN, (low < LowerBand and close > LowerBand), open[-1], 1, tickColor = GetColor(9), arrowColor = GetColor(9));
#Target
AddOrder(OrderType.SELL_TO_CLOSE, (high crosses above UpperBand[-1]), UpperBand, 1, tickColor = GetColor(9), arrowColor = GetColor(9));
#Stop
AddOrder(OrderType.SELL_TO_CLOSE, close < EntryPrice () - (ATR * stop), EntryPrice () - (ATR * stop) , 1, tickColor = GetColor(9), arrowColor = GetColor(9));

#Short Orders
AddOrder(OrderType.SELL_TO_OPEN, (High > UpperBand and close < UpperBand), open[-1], 1, tickColor = GetColor(9), arrowColor = GetColor(9));

#Target
AddOrder(OrderType.BUY_TO_CLOSE, (low crosses below lowerBand[-1]), LowerBand, 1, tickColor = GetColor(9), arrowColor = GetColor(9));
#stop
AddOrder(OrderType.BUY_TO_CLOSE, close > EntryPrice () + (ATR * stop), EntryPrice () + (ATR * stop) , 1, tickColor = GetColor(9), arrowColor = GetColor(9));